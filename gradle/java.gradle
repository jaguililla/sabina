/*
 * Copyright © 2015 Juan José Aguililla. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file
 * except in compliance with the License. You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied. See the License for the specific language governing permissions
 * and limitations under the License.
 */

/*
 * TODO Change compiler warnings for errors
 */

apply plugin: 'java'
apply plugin: 'jacoco'

defaultTasks 'check'

processResources {
    filterPatterns.split(",").each {pattern ->
        filesMatching (pattern.trim ()) {
            filter {
                it.replace ('${project.version}', project.version)
            }
        }
    }
}

clean << {
    delete "$buildDir"
    delete "${project.projectDir}/logs" // TODO Fix this
}

task wipe (dependsOn: 'clean') << {
    delete ".gradle"
}

repositories {
    jcenter ()
    mavenLocal()
}

configurations {
    umlgraphDoclet
}

dependencies {
    umlgraphDoclet 'org.umlgraph:umlgraph:5.6.6'
}

javadoc {
    options.memberLevel = 'PACKAGE'
    options.docletpath = configurations.umlgraphDoclet.files.asType(List)
    options.doclet = 'org.umlgraph.doclet.UmlGraphDoc'

    options.addBooleanOption('views', true)
    options.addBooleanOption('attributes', true)
    options.addBooleanOption('enumconstants', true)
    options.addBooleanOption('inferrel', true)
    options.addBooleanOption('inferdep', true)
    options.addBooleanOption('types', true)
    options.addBooleanOption('visibility', true)

    options.addStringOption('hide', 'java.*')
    options.addStringOption('collpackages', 'java.util.*')
    options.addStringOption('nodefontsize', '9')
    options.addStringOption('nodefontpackagesize', '7')
    options.addStringOption('link', 'http://download.oracle.com/javase/8/docs/api/')

    // TODO Not used because no string parameter is used
    options.addStringOption('Xdoclint:none') // Check http://docs.oracle.com/javase/8/docs/technotes/tools/unix/javadoc.html
}

test {
    useTestNG ()

    testLogging {
        showStandardStreams = System.getProperty ("show.output") != null
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // Coveralls plugin depends on xml format report
        html.enabled = true
    }
}
