/*
 * Copyright (c) 2015. Lorem ipsum dolor sit amet, consectetur adipiscing elit.
 * Morbi non lorem porttitor neque feugiat blandit. Ut vitae ipsum eget quam lacinia accumsan.
 * Etiam sed turpis ac ipsum condimentum fringilla. Maecenas magna.
 * Proin dapibus sapien vel ante. Aliquam erat volutpat. Pellentesque sagittis ligula eget metus.
 * Vestibulum commodo. Ut rhoncus gravida arcu.
 */

/*
 * Some properties are defined in 'gradle.properties'
 */
subprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    apply plugin: 'java'
    apply plugin: 'groovy'

    apply plugin: 'jacoco'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    group = 'sabina'
    version = projectVersion
    description = 'A Sinatra inspired Java web framework'

    // TODO To be completed
//    processResources {
//        filesMatching('**/config.properties') {
//            filter {
//                it.replace('localhost', project.property('myhost'))
//            }
//        }
//    }

    repositories {
        jcenter ()
    }

    configurations {
        deployerJars
    }

    dependencies {
        testCompile 'org.apache.httpcomponents:httpclient:4.3.3'
        testCompile 'org.testng:testng:6.8.21'
    }

    test {
        useTestNG ()
        include 'sabina/**'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // Coveralls plugin depends on xml format report
            html.enabled = true
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            mavenJava (MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier 'sources'
                }
            }
        }
        repositories {
            maven {
                url 'https://api.bintray.com/maven/jamming/maven/Sabina'
                credentials {
                    // Set 'bintrayKey' in '~/.gradle/gradle.properties'
                    def hasBintrayKey = project.hasProperty ("${project.name}BintrayKey")
                    def bintrayKey = hasBintrayKey?
                        project.property ("${project.name}BintrayKey") : "UNDEFINED"

                    username 'jamming'
                    password bintrayKey
                }
            }
        }
    }
}

defaultTasks 'check'

wrapper {
    gradleVersion = '2.3'
    jarFile = "${project.projectDir}/" + wrapperPath + '/wrapper.jar'
    scriptFile = "${project.projectDir}/" + wrapperPath + '/wrapper'
}
