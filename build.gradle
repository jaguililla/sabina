/*
 * Copyright © 2014 Juan José Aguililla. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file
 * except in compliance with the License. You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied. See the License for the specific language governing permissions
 * and limitations under the License.
 */

buildscript {
    ext {
        wrapperPath = "${project.projectDir}/gradle"
    }

    repositories {
        jcenter ()
    }

    dependencies {
        classpath 'org.freemarker:freemarker:2.3.19'
        classpath 'org.pegdown:pegdown:1.4.2'

        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1x'
    }
}

plugins {
    id 'me.champeau.jbake' version '0.2'
    id 'org.ajoberstar.github-pages' version '0.13.0-milestone.1'
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    apply plugin: 'com.github.kt3k.coveralls'

    group = 'sabina'
    version = '2.0.1-SNAPSHOT'
    description = 'A Sinatra inspired Java web framework'

    repositories {
        jcenter ()
    }

    configurations {
        deployerJars
    }

    dependencies {
        testCompile 'org.apache.httpcomponents:httpclient:4.3.3'
        testCompile 'org.testng:testng:6.8.8'
        testCompile 'junit:junit:4.11'
    }

    test {
        useTestNG ()
        include 'sabina/**'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // Coveralls plugin depends on xml format report
            html.enabled = true
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            mavenJava (MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier 'sources'
                }
            }
        }
        repositories {
            maven {
                url 'https://api.bintray.com/maven/jamming/maven/Sabina'
                credentials {
                    // Set 'bintrayKey' in '~/.gradle/gradle.properties'
                    def hasBintrayKey = Project.hasProperty ("${name}BintrayKey")
                    def bintrayKey = hasBintrayKey? Project.property ("${name}BintrayKey") : "UNDEFINED"

                    username 'jamming'
                    password bintrayKey
                }
            }
        }
    }
}

defaultTasks 'check'

jbake {
    input = file("$project.projectDir/site")
    output = file("$buildDir/site")
}

task site (dependsOn: 'jbake') {
    copy {
        from 'core/build/docs/javadoc/'
        into "$buildDir/site/core/"
        include ('**')
    }
    copy {
        from 'extra/build/docs/javadoc/'
        into "$buildDir/site/extra/"
        include ('**')
    }
}

githubPages {
    repoUri = 'https://github.com/jamming/sabina.git'
    pages {
        from (file ("$buildDir/site")) {
            into '.'
        }
    }
}

wrapper {
    gradleVersion = '2.2.1'
    jarFile = wrapperPath + '/wrapper.jar'
    scriptFile = wrapperPath + '/wrapper'
}
