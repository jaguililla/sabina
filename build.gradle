/*
 * Copyright © 2014 Juan José Aguililla. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file
 * except in compliance with the License. You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied. See the License for the specific language governing permissions
 * and limitations under the License.
 */

buildscript {
    repositories {
        jcenter ()
    }

    dependencies {
        classpath 'org.freemarker:freemarker:2.3.23'
        classpath 'org.pegdown:pegdown:1.5.0'
        classpath 'org.asciidoctor:asciidoctorj:1.5.2'
        classpath 'org.jbake:jbake-core:2.4.0'
        classpath 'me.champeau.gradle:jbake-gradle-plugin:0.2'
    }
}

plugins {
    id 'org.ajoberstar.github-pages' version '1.2.0'
    id "com.github.kt3k.coveralls" version "2.4.0"
}

apply from: 'gradle/wrapper.gradle'
apply from: 'gradle/site.gradle'

subprojects {
    apply from: "${project.rootDir}/gradle/java.gradle"
    apply from: "${project.rootDir}/gradle/bintray.gradle"

    apply plugin: 'groovy'

    dependencies {
        testCompile 'org.apache.httpcomponents:httpclient:4.3.3' // TODO Upgrade breaks tests
        testCompile 'org.testng:testng:6.9.6'
        testCompile 'org.codehaus.groovy:groovy:2.4.4'
    }
}

/*
 * Only takes care of HTTP module. Check issue #26
 */
coveralls {
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${projectDir}/http/build/reports/jacoco/test/jacocoTestReport.xml"
}

tasks.coveralls {
    onlyIf { System.env.'CI' }
}

githubPages {
    repoUri = githubUrl
    pages {
        from (file ("$buildDir/$projectSite")) {
            into '.'
        }
    }
}
