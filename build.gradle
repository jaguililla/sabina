/*
 * Copyright © 2014 Juan José Aguililla. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file
 * except in compliance with the License. You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied. See the License for the specific language governing permissions
 * and limitations under the License.
 */

import static JavaVersion.VERSION_1_8

subprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'java'

    apply plugin: 'jacoco'
    apply plugin: 'com.github.kt3k.coveralls'

    group = 'sabina'
    version = '2.0.1-SNAPSHOT'
    description = 'A Sinatra inspired Java web framework'

    sourceCompatibility = VERSION_1_8
    targetCompatibility = VERSION_1_8

    repositories {
        mavenCentral ()
    }

    dependencies {
        testCompile 'org.apache.httpcomponents:httpclient:4.3.3'
        testCompile 'org.testng:testng:6.8.8'
        testCompile 'junit:junit:4.11'
    }

    test {
        useTestNG ()
        include 'sabina/**'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // Coveralls plugin depends on xml format report
            html.enabled = true
        }
    }
}

apply plugin: 'me.champeau.jbake'
apply plugin: 'github-pages'

jbake {
    input = file("$project.projectDir/site")
    output = file("$buildDir/site")
}

githubPages {
    repoUri = 'https://github.com/jamming/sabina.git'
    pages {
        from (file ("$buildDir/site")) {
            into '.'
        }
    }
}

task wrapper (type: Wrapper) {
    gradleVersion = '2.1'
}

/*
 * Check 'http://melix.github.io/blog//2014/02/hosting-jbake-github.html' if any problem arises
 * In the article uses TWO gradle files are used
 */
buildscript {
    repositories {
        mavenLocal ()
        jcenter ()
    }

    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.9.0'
        classpath 'me.champeau.gradle:jbake-gradle-plugin:0.2'
        classpath 'org.freemarker:freemarker:2.3.19'
        classpath 'org.pegdown:pegdown:1.4.2'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.0'
    }
}
