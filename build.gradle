
buildscript {
    repositories {
        jcenter ()
    }

    dependencies {
        classpath 'org.freemarker:freemarker:2.3.23'
        classpath 'org.pegdown:pegdown:1.5.0'
        classpath 'org.asciidoctor:asciidoctorj:1.5.2'
        classpath 'org.jbake:jbake-core:2.4.0'
        classpath 'me.champeau.gradle:jbake-gradle-plugin:0.2'
    }
}

plugins {
    id 'org.ajoberstar.github-pages' version '1.2.0'
    id "com.github.kt3k.coveralls" version "2.4.0"
}

apply from: 'gradle/site.gradle'

subprojects {
    apply from: "${project.rootDir}/gradle/java.gradle"

    apply plugin: 'groovy'

    dependencies {
        compile "org.slf4j:slf4j-api:1.7.12"

        testCompile 'org.apache.httpcomponents:httpclient:4.3.3' // TODO Upgrade breaks tests
        testCompile 'org.testng:testng:6.9.6'
        testCompile 'org.codehaus.groovy:groovy:2.4.4'
        testCompile "ch.qos.logback:logback-classic:1.1.3"
    }

    test {
        useTestNG ()
    }
}

/*
 * Only takes care of HTTP module. Check issue #26
 */
coveralls {
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${projectDir}/http/build/reports/jacoco/test/jacocoTestReport.xml"
}

tasks.coveralls {
    onlyIf { System.env.'CI' }
}

githubPages {
    repoUri = githubUrl
    pages {
        from (file ("$buildDir/$projectSite")) {
            into '.'
        }
    }
}
